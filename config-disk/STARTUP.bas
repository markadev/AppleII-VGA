 5  TEXT
 10  D$ = CHR$ (4): SLOT = 0
 20  REM '''GET SLOT NUMBER
 21  HOME: VTAB 12: INPUT "APPLE II VGA CARD SLOT (1-7): ";SLOT
 22  IF SLOT < 1 THEN  GOTO 50
 23  IF SLOT > 7 THEN  GOTO 50
 24  BASEADDR = 49280 + 16 * SLOT
 25  GOTO 100
 50  REM '''INVALID SLOT
 51  HOME: VTAB 12: HTAB 13: PRINT "[INVALID SLOT]": GOSUB 3600
 52  GOTO 20

 100  REM '''MAIN MENU
 101  HOME: PRINT "APPLE II VGA CONFIGURATION": GOSUB 3400: PRINT ""
 102  PRINT "1  SCANLINES          2  MONOCHROME"
 103  PRINT "3  TEXT CHARSET       4  ][+ VIDEX"
 104  PRINT "5  HIRES SMOOTHING"
 105  PRINT "6  COLOR PATTERNS": PRINT ""
 110  PRINT "11 ABOUT"
 111  PRINT "12 LOAD DEFAULT CONFIG": PRINT "13 LOAD SAVED CONFIG"
 112  PRINT "14 SAVE AND QUIT": PRINT "15 QUIT W/OUT SAVING"
 120  VTAB 22: GOSUB 3400: INPUT "PLEASE ENTER YOUR SELECTION: ";CH
 200  IF CH < 1 THEN  GOSUB 3500
 201  IF CH = 1 THEN  GOSUB 500
 202  IF CH = 2 THEN  GOSUB 600
 203  IF CH = 3 THEN  GOSUB 800
 204  IF CH = 4 THEN  GOSUB 1000
 205  IF CH = 5 THEN  GOSUB 1300
 206  IF CH = 6 THEN  GOSUB 1200
 207  IF CH = 7 THEN  GOSUB 3500
 208  IF CH = 8 THEN  GOSUB 3500
 209  IF CH = 9 THEN  GOSUB 3500
 210  IF CH = 10 THEN  GOSUB 3500
 211  IF CH = 11 THEN  GOSUB 3700
 212  IF CH = 12 THEN  GOSUB 3800
 213  IF CH = 13 THEN  GOSUB 3900
 214  IF CH = 14 THEN  GOTO 4000
 215  IF CH = 15 THEN  GOTO 4050
 216  IF CH > 15 THEN  GOSUB 3500
 250  GOTO 100

 500  REM '''SCANLINES MENU
 501  HOME: PRINT "SCANLINE CONFIGURATION OPTIONS": GOSUB 3400: PRINT ""
 502  PRINT "1  ENABLE           2  DISABLE"
 503  VTAB 20: PRINT "3  RETURN TO MAIN MENU"
 504  VTAB 22: GOSUB 3400: INPUT "PLEASE ENTER YOUR SELECTION: ";CI
 510  IF CI < 1 THEN  GOSUB 3500
 511  IF CI = 1 THEN  GOSUB 550
 512  IF CI = 2 THEN  GOSUB 560
 513  IF CI = 3 THEN  RETURN
 514  IF CI > 3 THEN  GOSUB 3500
 515  GOTO 500
 550  REM '''SCANLINES ON
 551  POKE BASEADDR + 0,1: RETURN
 560  REM '''SCANLINES OFF
 561  POKE BASEADDR + 0,2: RETURN

 600  REM '''MONOCHROME CONFIG MENU
 601  HOME: PRINT "MONOCHROME CONFIGURATION OPTIONS": GOSUB 3400: PRINT ""
 602  PRINT "FOREGROUND COLOR"
 603  PRINT "  1  WHITE": PRINT "  2  GREEN": PRINT "  3  AMBER": PRINT ""
 604  PRINT "BACKGROUND COLOR"
 605  PRINT "  4  BLACK": PRINT "  5  GREEN": PRINT "  6  AMBER": PRINT ""
 606  PRINT "7  ALWAYS USE COLOR FOR TEXT": PRINT "8  ONLY USE COLOR IN SOFT-MONO MODE": PRINT ""
 607  PRINT "9  ENABLE SOFT-MONO MODE": PRINT "10 DISABLE SOFT-MONO MODE": PRINT ""
 608  VTAB 20: PRINT "11 RETURN TO MAIN MENU"
 609  VTAB 22: GOSUB 3400: INPUT "PLEASE ENTER YOUR SELECTION: ";CI
 620  IF CI < 1 THEN  GOSUB 3500
 621  IF CI = 1 THEN  GOSUB 650
 622  IF CI = 2 THEN  GOSUB 660
 623  IF CI = 3 THEN  GOSUB 670
 624  IF CI = 4 THEN  GOSUB 680
 625  IF CI = 5 THEN  GOSUB 690
 626  IF CI = 6 THEN  GOSUB 700
 627  IF CI = 7 THEN  GOSUB 710
 628  IF CI = 8 THEN  GOSUB 720
 629  IF CI = 9 THEN  GOSUB 730
 630  IF CI = 10 THEN  GOSUB 740
 631  IF CI = 11 THEN  RETURN
 632  IF CI > 11 THEN  GOSUB 3500
 633  GOTO 600
 650  REM '''MONOCHROME FOREGROUND WHITE
 651  POKE BASEADDR + 1,1: RETURN
 660  REM '''MONOCHROME FOREGROUND GREEN
 661  POKE BASEADDR + 1,2: RETURN
 670  REM '''MONOCHROME FOREGROUND AMBER
 671  POKE BASEADDR + 1,3: RETURN
 680  REM '''MONOCHROME BACKGROUND BLACK
 681  POKE BASEADDR + 1,16: RETURN
 690  REM '''MONOCHROME BACKGROUND GREEN
 691  POKE BASEADDR + 1,32: RETURN
 700  REM '''MONOCHROME BACKGROUND AMBER
 701  POKE BASEADDR + 1,48: RETURN
 710  REM '''ALWAYS USE COLOR FOR TEXT
 711  POKE BASEADDR + 1,64: RETURN
 720  REM '''ONLY USE COLOR IN SOFT-MONO MODE
 721  POKE BASEADDR + 1,128: RETURN
 730  REM '''MONOCHROME ON
 731  POKE 49185,128: RETURN
 740  REM '''MONOCHROME OFF
 741  POKE 49185,0: RETURN

 800  REM '''CHARSET CONFIG MENU
 801  HOME: PRINT "CHARSET CONFIGURATION OPTIONS": GOSUB 3400: PRINT ""
 802  PRINT "1  CHARACTER SET VIEWER"
 803  PRINT "2  LOAD A BUILT-IN CHARSET"
 804  VTAB 20: PRINT "3  RETURN TO MAIN MENU"
 805  VTAB 22: GOSUB 3400: INPUT "PLEASE ENTER YOUR SELECTION: ";CI
 820  IF CI < 1 THEN  GOSUB 3500
 821  IF CI = 1 THEN  GOSUB 850
 822  IF CI = 2 THEN  GOSUB 870
 823  IF CI = 3 THEN  RETURN
 824  IF CI > 3 THEN  GOSUB 3500
 825  GOTO 800
 850  REM '''CHARACTER SET VIEWER
 851  HOME
 852  FOR R = 0 TO 7
 853  FOR C = 0 TO 31
 854  POKE R * 128 + 1024 + C,R * 32 + C
 855  NEXT C,R
 856  VTAB 21: GOSUB 3400: PRINT "PRESS ANY KEY TO CONTINUE...": GET A$
 857  RETURN
 870  REM '''LOAD A BUILT-IN CHARSET
 871  HOME: INPUT "ENTER CHARSET (0-15): ";CS
 872  IF CS < 0 THEN RETURN
 873  IF CS > 15 THEN RETURN
 874  POKE BASEADDR + 4,16+CS: RETURN

 1000  REM '''][+ VIDEX CONFIG MENU
 1001  HOME: PRINT "][+ VIDEX SUPPORT (SLOT 3 ONLY)": GOSUB 3400: PRINT ""
 1002  PRINT "1  ENABLE VIDEX 80 COLUMN SUPPORT"
 1003  PRINT "2  DISABLE VIDEX 80 COLUMN SUPPORT"
 1004  VTAB 20: PRINT "3  RETURN TO MAIN MENU"
 1005  VTAB 22: GOSUB 3400: INPUT "PLEASE ENTER YOUR SELECTION: ";CI
 1020  IF CI < 1 THEN  GOSUB 3500
 1021  IF CI = 1 THEN  GOSUB 1050
 1022  IF CI = 2 THEN  GOSUB 1060
 1023  IF CI = 3 THEN  RETURN
 1024  IF CI > 3 THEN  GOSUB 3500
 1025  GOTO 1000
 1050  REM '''][+ VIDEX ON
 1051  POKE BASEADDR + 0,4: RETURN
 1060  REM '''][+ VIDEX OFF
 1061  POKE BASEADDR + 0,8: RETURN

 1200  REM '''COLOR PATTERNS
 1210  GR: HOME
 1220  FOR I = 0 TO 31: COLOR= I / 2: VLIN 0,39 AT I: NEXT I: FOR I = 0 TO 14 STEP 2: PRINT  TAB( I * 2 + 1);I;: NEXT I: PRINT: FOR I = 1 TO 15 STEP 2: PRINT  TAB( I * 2 + 1);I;: NEXT I
 1230  PRINT "": GET A$: TEXT: RETURN

 1300  REM '''HIRES SMOOTHING
 1301  HGR: GOSUB 3300
 1310  HOME: VTAB 21: PRINT "HIRES SMOOTHING"
 1311  PRINT " 1 ENABLE      2 DISABLE      3 GO BACK": PRINT ""
 1312  INPUT "PLEASE ENTER YOUR SELECTION: ";CI
 1313  IF CI = 1 THEN  GOSUB 1350
 1314  IF CI = 2 THEN  GOSUB 1360
 1315  IF CI = 3 THEN  GOTO 1399
 1316  GOTO 1310
 1350  REM '''ENABLE SMOOTH HIRES
 1351  POKE 49194,128: RETURN
 1360  REM '''DISABLE SMOOTH HIRES
 1361  POKE 49194,0: RETURN
 1399  TEXT: RETURN

 3300  REM '''DRAW TRIANGLES
 3310  HCOLOR=3: HPLOT 4,4 TO 275,4: HPLOT 275,4 TO 275,155: HPLOT 4,4 TO 4,155: HPLOT 4,155 TO 275,155
 3320  HCOLOR=1: X=40: Y=20: HPLOT X,Y TO X-10,Y+20: HPLOT X-10,Y+20 TO X+10,Y+20: HPLOT X,Y TO X+10,Y+20
 3330  HCOLOR=2: X=100: Y=60: HPLOT X,Y TO X-10,Y+20: HPLOT X-10,Y+20 TO X+10,Y+20: HPLOT X,Y TO X+10,Y+20
 3340  HCOLOR=5: X=190: Y=40: HPLOT X,Y TO X-10,Y+20: HPLOT X-10,Y+20 TO X+10,Y+20: HPLOT X,Y TO X+10,Y+20
 3350  HCOLOR=6: X=240: Y=100: HPLOT X,Y TO X-10,Y+20: HPLOT X-10,Y+20 TO X+10,Y+20: HPLOT X,Y TO X+10,Y+20
 3360  HCOLOR=3: X=70: Y=120: HPLOT X,Y TO X-10,Y+20: HPLOT X-10,Y+20 TO X+10,Y+20: HPLOT X,Y TO X+10,Y+20
 3370  RETURN
 3400  REM '''PRINT HLINE
 3410  PRINT "---------------------------------------": RETURN
 3500  REM '''INVALID OPTION
 3510  HOME: VTAB 12: HTAB 12: PRINT "[INVALID OPTION]": GOSUB 3600: RETURN
 3600  REM '''DELAY
 3610  FOR I = 1 TO 1000: NEXT I: RETURN

 3700  REM '''ABOUT
 3701  HOME: PRINT "APPLE II VGA": GOSUB 3400: PRINT "COPYRIGHT (C) 2021-2025 MARK AIKENS": PRINT "COPYRIGHT (C) 2022-2023 DAVID KUDER"
 3702  PRINT "": PRINT "": PRINT "THIS PROJECT IS A VGA CARD FOR APPLE II": PRINT "COMPUTERS TO  OUPUT A CRISP  RGB SIGNAL": PRINT "TO A VGA MONITOR  INSTEAD OF  HAVING TO": PRINT "RELY ON  THE COMPOSITE OUTPUT.  THIS IS"
 3703  PRINT "ACCOMPLISHED BY  SNOOPING THE  6502 BUS": PRINT "AND CREATING A SHADOW COPY OF THE VIDEO": PRINT "MEMORY WITHIN A RASPBERRY PI PICO, THEN": PRINT "PROCESSING THE VIDEO MEMORY CONTENTS TO"
 3704  PRINT "OUTPUT A 'PERFECT' SIGNAL.": PRINT "": PRINT "": HTAB 6: PRINT "[MORE DETAILS AVAILABLE AT]": PRINT "HTTPS://GITHUB.COM/MARKADEV/APPLEII-VGA"
 3750  VTAB 21: GOSUB 3400: PRINT "PRESS ANY KEY TO CONTINUE...": GET A$
 3751  RETURN

 3800  REM '''LOAD DEFAULT CONFIGURATION
 3810  POKE BASEADDR + 4,0: RETURN
 3900  REM '''LOAD SAVED CONFIGURATION
 3910  POKE BASEADDR + 4,1: RETURN
 4000  REM '''SAVE AND EXIT
 4010  POKE BASEADDR + 4,2
 4020  FOR I = 1 TO 500: NEXT I
 4030  HOME : PRINT "YOUR SETTINGS WERE SAVED TO FLASH": GOTO 4100
 4050  REM ''' EXIT W/OUT SAVING
 4060  HOME : PRINT "YOUR SETTINGS WERE NOT SAVED TO FLASH": PRINT "BUT WILL REMAIN ACTIVE UNTIL THE NEXT": PRINT "POWER CYCLE"
 4100  REM '''END
